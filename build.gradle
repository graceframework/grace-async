buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:$grailsVersion"
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}
version project.projectVersion

ext {
    commonBuild = 'https://raw.githubusercontent.com/grails/grails-common-build/master'
}

subprojects {

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    if(isGrailsPlugin) {
        group "org.grails.plugins"
    }
    else {
        group "org.grails"
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }

    if(it.projectDir.path.endsWith("examples/${it.name}".toString())) {
        apply plugin:"org.grails.grails-web"
    }
    else if(isGrailsPlugin) {
        apply from:"${commonBuild}/common-plugin.gradle"
    }
    else {
        apply from:"${commonBuild}/common-project.gradle"
    }

    dependencies {
        compile 'javax.annotation:javax.annotation-api:1.3.2'
    }
}

apply from:"${commonBuild}/common-publishing.gradle"
apply from:"${commonBuild}/common-docs.gradle"
